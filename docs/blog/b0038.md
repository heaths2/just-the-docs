---
layout: default
title: Windows 10 정품 인증
parent: Blog
grand_parent: Category
permalink: docs/category/blog/b0038
child_nav_order: desc
---

개요

> - `slmgr`도구를 사용하여 Windows 10 정품 인증을 한다.
{: .new }

### `slmgr` 도구 사용법

{: .important }
> - 관리자 모드
```bash
# 윈도우 방화벽 비활성화
netsh advfirewall set allprofiles state off
# 윈도우 방화벽 활성화
netsh advfirewall set allprofiles state on
```

```bash
C:\Windows\system32>slmgr /ipk W269N-WFGWX-YVC9B-4J6C9-T83GX
C:\Windows\system32>slmgr /skms kms8.msguides.com
C:\Windows\system32>slmgr /ato
C:\Windows\system32>slmgr /xpr
```

![image](https://user-images.githubusercontent.com/36792594/195773136-a66c402a-6241-4003-9a28-fd97fd26194f.png)
![image](https://user-images.githubusercontent.com/36792594/195773257-2562a7f4-1008-45b8-9da6-c75cd37c6af7.png)
![image](https://user-images.githubusercontent.com/36792594/195773400-f9231cbe-6a5b-424a-99f3-801b8b886a1e.png)
![image](https://user-images.githubusercontent.com/36792594/195773826-0e5087fe-6407-438f-95cc-7a56667a6163.png)


Windows 라이선스 키 추출

- [Windows 10 Extract License Keys.txt](https://github.com/heaths2/heaths2.github.io/files/9783181/Windows.10.Extract.License.Keys.txt)

```bash
Option Explicit 

Dim objshell,path,DigitalID, Result 
Set objshell = CreateObject("WScript.Shell")
'Set registry key path
Path = "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\"
'Registry key value
DigitalID = objshell.RegRead(Path & "DigitalProductId")
Dim ProductName,ProductID,ProductKey,ProductData
'Get ProductName, ProductID, ProductKey
ProductName = "Product Name: " & objshell.RegRead(Path & "ProductName")
ProductID = "Product ID: " & objshell.RegRead(Path & "ProductID")
ProductKey = "Installed Key: " & ConvertToKey(DigitalID) 
ProductData = ProductName  & vbNewLine & ProductID  & vbNewLine & ProductKey
'Show messbox if save to a file 
If vbYes = MsgBox(ProductData  & vblf & vblf & "Save to a file?", vbYesNo + vbQuestion, "BackUp Windows Key Information") then
   Save ProductData 
End If



'Convert binary to chars
Function ConvertToKey(Key)
    Const KeyOffset = 52
    Dim isWin8, Maps, i, j, Current, KeyOutput, Last, keypart1, insert
    'Check if OS is Windows 8
    isWin8 = (Key(66) \ 6) And 1
    Key(66) = (Key(66) And &HF7) Or ((isWin8 And 2) * 4)
    i = 24
    Maps = "BCDFGHJKMPQRTVWXY2346789"
    Do
       Current= 0
        j = 14
        Do
           Current = Current* 256
           Current = Key(j + KeyOffset) + Current
           Key(j + KeyOffset) = (Current \ 24)
           Current=Current Mod 24
            j = j -1
        Loop While j >= 0
        i = i -1
        KeyOutput = Mid(Maps,Current+ 1, 1) & KeyOutput
        Last = Current
    Loop While i >= 0 
    keypart1 = Mid(KeyOutput, 2, Last)
    insert = "N"
    KeyOutput = Replace(KeyOutput, keypart1, keypart1 & insert, 2, 1, 0)
    If Last = 0 Then KeyOutput = insert & KeyOutput
    ConvertToKey = Mid(KeyOutput, 1, 5) & "-" & Mid(KeyOutput, 6, 5) & "-" & Mid(KeyOutput, 11, 5) & "-" & Mid(KeyOutput, 16, 5) & "-" & Mid(KeyOutput, 21, 5)
   
    
End Function
'Save data to a file
Function Save(Data)
    Dim fso, fName, txt,objshell,UserName
    Set objshell = CreateObject("wscript.shell")
    'Get current user name 
    UserName = objshell.ExpandEnvironmentStrings("%UserName%") 
    'Create a text file on desktop 
    fName = "C:\Users\" & UserName & "\Desktop\WindowsKeyInfo.txt"
    Set fso = CreateObject("Scripting.FileSystemObject")
    Set txt = fso.CreateTextFile(fName)
    txt.Writeline Data
    txt.Close
End Function
```

![image](https://user-images.githubusercontent.com/36792594/195779926-2daf5e28-1fbf-4915-aa1f-138c8282d574.png)
![image](https://user-images.githubusercontent.com/36792594/195780129-121b7f27-743f-458d-be3a-2d60b8fc5867.png)
![image](https://user-images.githubusercontent.com/36792594/195780524-40b81de2-64ac-4d75-baa0-6ce142309810.png)
