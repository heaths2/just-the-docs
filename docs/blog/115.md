---
layout: default
title: IPXE boot
parent: Blog
grand_parent: Category
permalink: docs/category/blog/115
child_nav_order: desc
---
# iPXE boot
<details open markdown="block">
  <summary>
    Table of contents
  </summary>
  {: .no_toc .text-delta .label .label-green }
1. TOC
{:toc}
</details>
---

## 개요

> - iPXE(Internet Preboot eXecution Environment) boot
> - [참조](https://gist.github.com/rikka0w0/50895b82cbec8a3a1e8c7707479824c1#file-ipxe_build-md)
{: .new }

### 도구 설치 및 IPXE 구성

- Install compiler and dependencies

```bash
apt install -y build-essential liblzma-dev isolinux git
```

- ipxe source code

```bash
git clone https://github.com/ipxe/ipxe.git
cd ipxe/src
```

- Enable NFS support

```bash
sed -i 's/#undef\tDOWNLOAD_PROTO_NFS/#define\tDOWNLOAD_PROTO_NFS/' config/general.h
```

- Enable Ping support

```bash
sed -i 's/\/\/#define\ PING_CMD/#define\ PING_CMD/' config/general.h
sed -i 's/\/\/#define\ IPSTAT_CMD/#define\ IPSTAT_CMD/' config/general.h
sed -i 's/\/\/#define\ REBOOT_CMD/#define\ REBOOT_CMD/' config/general.h
sed -i 's/\/\/#define\ POWEROFF/#define\ POWEROFF/' config/general.h
```

- Enable Console_CMD support

```bash
sed -i 's/\/\/#define\ CONSOLE_CMD/#define\ CONSOLE_CMD/' config/general.h
sed -i 's/\/\/#define[[:space:]]\+CONSOLE_FRAMEBUFFER/#define       CONSOLE_FRAMEBUFFER/' config/console.h
```

### 내장된 스크립트 생성

- Create the embedded script

```bash
#!ipxe
# DHCP
dhcp
chain tftp://${next-server}/main.ipxe
```

### IPXE 컴파일

- Compile IPXE

```bash
make bin/undionly.kpxe bin-x86_64-efi/ipxe.efi EMBED=/root/ipxe/src/embed.ipxe
```

### TFTP 루트에 bin/undionly.kpxe 업로드

```bash
mkdir -p /srv/tftp/ipxe/bin/
cp -av bin/undionly.kpxe bin-x86_64-efi/ipxe.efi /srv/tftp/ipxe/bin/
```

### TFTP 옵션 설정

```bash
# /etc/default/tftpd-hpa

TFTP_USERNAME="tftp"
TFTP_DIRECTORY="/srv/tftp"
TFTP_ADDRESS=":69"
TFTP_OPTIONS="--secure --create --list -vvv"
RUN_DAEMON="yes"
OPTIONS=" -l -s -r -blksize 1468 /srv/tftp"
```

- 서비스 재시작

```bash
systemctl restart tftpd-hpa.service
```

### DNS 서버 구성

- Configure DNS server

```bash
IPADD=$(hostname -I | awk '{print $1}')
IPGW=$(ip route | grep default | head -n 1 | awk '{print $3}')
IPADD_PREFIX=$(echo $IPADD | sed 's/\.[0-9]*$//')
cat << EOF >> /etc/dhcp/dhcpd.conf
subnet 192.168.0.0 netmask 255.255.255.0 {
 option routers                  $IPGW;
 option subnet-mask              255.255.255.0;
 option domain-name-servers      1.1.1.1, 8.8.8.8;
 option domain-name              "ipxe-boot.com";
 option time-offset              32400;     # Timezone: Asia/Seoul
 range $IPADD_PREFIX.100 $IPADD_PREFIX.200;
 next-server $IPADD;
 filename "ipxe/bin/undionly.kpxe";
}
EOF
```

### TFTP 루트에 main.ipxe 생성

<details markdown="block">
  <summary>
    코드
  </summary>
  {: .label .label-green }

```bash
#!ipxe
# Background Images
console --x 1024 --y 768 --picture http://boot.ipxe.org/ipxe.png --left 32 --right 32 --top 32 --bottom 48

# Set Color Fonts
set esc:hex 1b
set bold ${esc:string}[1m
set boldoff ${esc:string}[22m
set fg_off ${esc:string}[0m
set fg_red ${esc:string}[31m
set fg_gre ${esc:string}[32m
set fg_cya ${esc:string}[36m
set fg_whi ${esc:string}[37m

# Set NFS strings
set nfs-server          ${next-server}
set nfs-mount           /srv/tftp
set nfs-path            nfs://${nfs-server}${nfs-mount}
set nfs-root            ${nfs-server}:${nfs-mount}

# HTTP and iSCSI
set iscsi-server        ${next-server}
set http-root           http://${next-server}:3259

:start
menu iPXE boot menu options
item --gap --                   ------------------------- Local boot options ------------------------------
item            localboot               Boot to local drive
item --gap --                   ------------------------- Network boot options ----------------------------
item            ubuntu22.04             Install Ubuntu 22.04
item            ubuntu20.04             Install Ubuntu 20.04
item            windows10               Install Windows10
item            restoredisk             Install Clonezilla Restoredisk xm-Mail20_G9
item --gap --                   ------------------------- HPE options -------------------------------------
item            ssa2.60                 Go to HPE Smart Storage Administrator 2.60
item            ssa4.15                 Go to HPE Smart Storage Administrator 4.15
item            gen8                    Go to HPE Service Pack for ProLiant Gen8
item            gen9                    Go to HPE Service Pack for ProLiant Gen9
item --gap --                   ------------------------- Advanced options --------------------------------
item --key c    clonezilla              Go to Clonezilla Live
item --key s    shell                   Go to iPXE Shell
item --key r    reboot                  Reboot
item
item --key x    exit                    Exit iPXE and continue BIOS boot
choose --default localboot --timeout 10000 target && goto ${target}

:localboot
echo ${fg_gre}Continue${fg_off} booting to local drive
goto exit

:shell
echo Type "exit" to return to menu
shell
goto start

:reboot
reboot

:exit
exit

###
### Custom menu entries
###

:clonezilla
kernel clonezilla/vmlinuz
initrd clonezilla/initrd.img
imgargs vmlinuz boot=live username=user union=overlay components noswap noprompt vga=791 keyboard-layouts=us locales=en_US.UTF-8 fetch=tftp://${nfs-server}/clonezilla/filesystem.squashfs
boot

:restoredisk
kernel clonezilla/vmlinuz
initrd clonezilla/initrd.img
imgargs vmlinuz boot=live username=user union=overlay config components quiet noswap edd=on nomodeset enforcing=0 noeject vga=791 fetch=tftp://${nfs-server}/clonezilla/filesystem.squashfs ocs_prerun="dhclient -v" ocs_prerun1="echo '1234' | sshfs root@${nfs-server}:/home/partimag /home/partimag -p 22 -o noatime -o ssh_command='ssh -oStrictHostKeyChecking=No' -o password_stdin" ocs_live_run="/usr/sbin/ocs-sr -g auto -e1 auto -e2 -r -j2 -icds -k1 -p reboot restoredisk xm-Mail20_G9 sda" keyboard-layouts=NONE ocs_live_batch="no" locales="en_US.UTF-8" nolocales
boot

:ubuntu22.04
kernel ubuntu22.04/casper/vmlinuz
initrd ubuntu22.04/casper/initrd
imgargs vmlinuz initrd=initrd vga=791 ip=dhcp nfsroot=${nfs-root}/ubuntu22.04 netboot=nfs boot=casper maybe-ubiquity quiet splash ---
boot

:ubuntu20.04
kernel ubuntu20.04/casper/vmlinuz
initrd ubuntu20.04/casper/initrd
imgargs vmlinuz initrd=initrd vga=791 ip=dhcp nfsroot=${nfs-root}/ubuntu20.04 netboot=nfs boot=casper maybe-ubiquity quiet splash ---
boot

:ssa2.60
kernel ssa2.60/system/vmlinuz initrd=initrd.img rootfstype=ramfs
initrd ssa2.60/system/initrd.img
imgargs vmlinuz initrd=initrd.img media=net rw root=/dev/ram0 ramdisk_size=257144 init=/init loglevel=3 ide=nodma ide=noraid pnpbios=off vga=791 splash=silent showopts TYPE=MANUAL iso1mnt=/srv/tftp/ssa2.60 iso1=nfs://${nfs-server}/srv/tftp/ssa2.60/ssaoffline-2.60-18.0.iso iso1opts=timeo=120,nolock,bg,ro
boot

:gen8
kernel spp8/system/vmlinuz initrd=initrd.img rootfstype=ramfs
initrd spp8/system/initrd.img
imgargs vmlinuz media=net rw ramdisk_size=1099688 root=/dev/ram0 init=/bin/init iso1mnt=/mnt/bootdevice loglevel=3 ide=nodma ide=noraid nopat pnpbios=off vga=791 splash=silent hp_fibre showopts noexec32=off numa=off nox2apic ipappend 2 TYPE=MANUAL AUTOPOWEROFFONSUCCESS=no AUTOREBOOTONSUCCESS=no iso1mnt=/srv/tftp/spp8 iso1=nfs://${nfs-server}/srv/tftp/spp8/spp8.iso iso1opts=timeo=120,nolock,bg,ro
boot

:gen9
kernel spp9/system/vmlinuz initrd=initrd.img rootfstype=ramfs
initrd spp9/system/initrd.img
imgargs vmlinuz media=net rw ramdisk_size=1099688 root=/dev/ram0 init=/bin/init iso1mnt=/mnt/bootdevice loglevel=3 ide=nodma ide=noraid nopat pnpbios=off vga=791 splash=silent hp_fibre showopts noexec32=off numa=off nox2apic ipappend 2 TYPE=MANUAL AUTOPOWEROFFONSUCCESS=no AUTOREBOOTONSUCCESS=no iso1mnt=/srv/tftp/spp9 iso1=nfs://${nfs-server}/srv/tftp/spp9/spp9.iso iso1opts=timeo=120,nolock,bg,ro
boot
```
  
</details>
